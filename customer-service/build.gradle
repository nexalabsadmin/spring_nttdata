plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'ec.com.nttdata'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.kafka:spring-kafka'

    // H2 Database
    runtimeOnly 'com.h2database:h2'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // MapStruct (updated version)
    implementation 'org.mapstruct:mapstruct:1.6.2'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.2'

    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.springframework.boot:spring-boot-starter-security'
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
    exclude '**/ec/com/nttdata/customer_service/CustomerServiceApplication/**/*IntegrationTest*'
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
    afterEvaluate {
        getClassDirectories().setFrom(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/ec/com/nttdata/customer_service/CustomerServiceApplication*',
                    '**/ec/com/nttdata/customer_service/dto/**',
                    '**/ec/com/nttdata/customer_service/mapper/**',
                    '**/ec/com/nttdata/customer_service/model/**',
                    '**/ec/com/nttdata/customer_service/repository/**',
                    '**/ec/com/nttdata/customer_service/config/**',
                    '**/ec/com/nttdata/customer_service/exception/**',
                    '**/ec/com/nttdata/customer_service/util/**',
            ])
        })
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.85
            }
            excludes = [
                    'ec.com.nttdata.customer_service.CustomerServiceApplication',
                    'ec.com.nttdata.customer_service.dto.**',
                    'ec.com.nttdata.customer_service.mapper.**',
                    'ec.com.nttdata.customer_service.model.**',
                    'ec.com.nttdata.customer_service.repository.**',
                    'ec.com.nttdata.customer_service.config.**',
                    'ec.com.nttdata.customer_service.exception.**',
                    'ec.com.nttdata.customer_service.util.**',
            ]
        }
    }
}

check.dependsOn jacocoTestCoverageVerification